defmodule ExRatatui.BridgeTest do
  use ExUnit.Case, async: true

  alias ExRatatui.Bridge
  alias ExRatatui.Layout.Rect
  alias ExRatatui.Session
  alias ExRatatui.Style
  alias ExRatatui.Text.{Line, Span}
  alias ExRatatui.Widgets.{Block, List, Paragraph, Popup, Table, Textarea, TextInput, WidgetList}

  test "encode_command encodes nested widgets through the shared bridge" do
    command =
      Bridge.encode_command(
        {%Popup{
           content: %Paragraph{
             text: "hello",
             style: %Style{fg: :green},
             block: %Block{title: "Inner", borders: [:all]}
           },
           block: %Block{title: "Outer", borders: [:all]},
           fixed_width: 20,
           fixed_height: 5
         }, %Rect{x: 1, y: 2, width: 30, height: 10}}
      )

    assert {
             %{
               "type" => "popup",
               "content" => %{
                 "type" => "paragraph",
                 "text" => %{
                   "lines" => [%{"spans" => [%{"content" => "hello"}]}]
                 }
               },
               "fixed_width" => 20,
               "fixed_height" => 5
             },
             %{"x" => 1, "y" => 2, "width" => 30, "height" => 10}
           } = command
  end

  test "draw raises a contextual validation error for text input without state" do
    terminal = ExRatatui.init_test_terminal(20, 5)
    on_exit(fn -> ExRatatui.Native.restore_terminal(terminal) end)

    assert_raise ArgumentError, "text_input.state is required and must be a reference", fn ->
      ExRatatui.draw(terminal, [{%TextInput{}, %Rect{x: 0, y: 0, width: 20, height: 1}}])
    end
  end

  test "session draw uses the same validation path" do
    session = Session.new(20, 5)
    on_exit(fn -> Session.close(session) end)

    assert_raise ArgumentError,
                 "widget_list.items must contain {widget, non_neg_integer()} tuples, got: {\"bad\", :height}",
                 fn ->
                   Session.draw(session, [
                     {%WidgetList{items: [{"bad", :height}]},
                      %Rect{x: 0, y: 0, width: 20, height: 5}}
                   ])
                 end
  end

  test "encode_command encodes list items through the rich-text pipeline" do
    command =
      Bridge.encode_command(
        {%List{
           items: [
             "plain",
             Span.new("span-item"),
             Line.new([Span.new("a"), Span.new("b")])
           ]
         }, %Rect{x: 0, y: 0, width: 10, height: 3}}
      )

    assert {
             %{
               "type" => "list",
               "items" => [
                 %{"lines" => [%{"spans" => [%{"content" => "plain"}]}]},
                 %{"lines" => [%{"spans" => [%{"content" => "span-item"}]}]},
                 %{
                   "lines" => [
                     %{"spans" => [%{"content" => "a"}, %{"content" => "b"}]}
                   ]
                 }
               ]
             },
             _rect
           } = command
  end

  test "encode_command encodes table rows and header through rich-text lines" do
    command =
      Bridge.encode_command(
        {%Table{
           rows: [
             [
               "plain",
               Span.new("styled", style: %Style{fg: :red})
             ],
             [
               Line.new([Span.new("a"), Span.new("b")]),
               "last"
             ]
           ],
           header: ["Name", Span.new("Value", style: %Style{modifiers: [:bold]})],
           widths: [{:length, 10}, {:length, 10}]
         }, %Rect{x: 0, y: 0, width: 30, height: 5}}
      )

    assert {
             %{
               "type" => "table",
               "rows" => [
                 [
                   %{"spans" => [%{"content" => "plain"}]},
                   %{"spans" => [%{"content" => "styled", "style" => %{"fg" => "red"}}]}
                 ],
                 [
                   %{"spans" => [%{"content" => "a"}, %{"content" => "b"}]},
                   %{"spans" => [%{"content" => "last"}]}
                 ]
               ],
               "header" => [
                 %{"spans" => [%{"content" => "Name"}]},
                 %{"spans" => [%{"content" => "Value"}]}
               ]
             },
             _rect
           } = command
  end

  test "encode_command encodes tab titles through rich-text lines" do
    alias ExRatatui.Widgets.Tabs

    command =
      Bridge.encode_command(
        {%Tabs{
           titles: [
             "Home",
             Span.new("Docs", style: %Style{fg: :cyan}),
             Line.new([
               Span.new("["),
               Span.new("X", style: %Style{modifiers: [:bold]}),
               Span.new("]")
             ])
           ]
         }, %Rect{x: 0, y: 0, width: 30, height: 1}}
      )

    assert {
             %{
               "type" => "tabs",
               "titles" => [
                 %{"spans" => [%{"content" => "Home"}]},
                 %{"spans" => [%{"content" => "Docs", "style" => %{"fg" => "cyan"}}]},
                 %{
                   "spans" => [
                     %{"content" => "["},
                     %{"content" => "X"},
                     %{"content" => "]"}
                   ]
                 }
               ]
             },
             _rect
           } = command
  end

  test "encode_command encodes block title through rich-text line" do
    command =
      Bridge.encode_command(
        {%Paragraph{
           text: "body",
           block: %Block{
             title:
               Line.new([
                 Span.new(" ok ", style: %Style{fg: :green}),
                 Span.new("Build", style: %Style{fg: :yellow, modifiers: [:bold]})
               ]),
             borders: [:all]
           }
         }, %Rect{x: 0, y: 0, width: 20, height: 3}}
      )

    assert {
             %{
               "type" => "paragraph",
               "block" => %{
                 "title" => %{
                   "spans" => [
                     %{"content" => " ok ", "style" => %{"fg" => "green"}},
                     %{
                       "content" => "Build",
                       "style" => %{"fg" => "yellow", "modifiers" => ["bold"]}
                     }
                   ]
                 }
               }
             },
             _rect
           } = command
  end

  test "encode_command accepts a plain string block title as a coerced line" do
    command =
      Bridge.encode_command(
        {%Paragraph{text: "body", block: %Block{title: "Hello", borders: [:all]}},
         %Rect{x: 0, y: 0, width: 20, height: 3}}
      )

    assert {
             %{
               "block" => %{
                 "title" => %{"spans" => [%{"content" => "Hello"}]}
               }
             },
             _rect
           } = command
  end

  test "encode_command omits block title key when nil" do
    command =
      Bridge.encode_command(
        {%Paragraph{text: "body", block: %Block{borders: [:all]}},
         %Rect{x: 0, y: 0, width: 20, height: 3}}
      )

    assert {%{"block" => block_map}, _rect} = command
    refute Map.has_key?(block_map, "title")
  end

  test "ExRatatui.encode_command/1 delegates to the shared bridge" do
    assert {
             %{
               "type" => "paragraph",
               "text" => %{
                 "lines" => [%{"spans" => [%{"content" => "delegated"}]}]
               }
             },
             %{"x" => 0, "y" => 0, "width" => 10, "height" => 2}
           } =
             ExRatatui.encode_command(
               {%Paragraph{text: "delegated"}, %Rect{x: 0, y: 0, width: 10, height: 2}}
             )
  end

  test "encode_command validates input shape and resource references" do
    rect = %Rect{x: 0, y: 0, width: 10, height: 2}

    assert_raise ArgumentError,
                 "expected a render command in the form {widget, %ExRatatui.Layout.Rect{}}, got: :bad",
                 fn ->
                   Bridge.encode_command(:bad)
                 end

    assert_raise ArgumentError,
                 "text_input.state is required and must be a reference, got: :bad",
                 fn ->
                   Bridge.encode_command({%TextInput{state: :bad}, rect})
                 end

    assert_raise ArgumentError, "textarea.state is required and must be a reference", fn ->
      Bridge.encode_command({%Textarea{}, rect})
    end

    assert_raise ArgumentError,
                 "textarea.state is required and must be a reference, got: :bad",
                 fn ->
                   Bridge.encode_command({%Textarea{state: :bad}, rect})
                 end
  end

  test "encode_command validates widgets, blocks, styles, colors, and constraints" do
    rect = %Rect{x: 0, y: 0, width: 10, height: 2}

    assert_raise ArgumentError, "unsupported widget struct: :bad", fn ->
      Bridge.encode_command({:bad, rect})
    end

    assert_raise ArgumentError,
                 "paragraph.block expected %ExRatatui.Widgets.Block{}, got: :bad",
                 fn ->
                   Bridge.encode_command({%Paragraph{text: "x", block: :bad}, rect})
                 end

    assert_raise ArgumentError, "paragraph.style expected %ExRatatui.Style{}, got: :bad", fn ->
      Bridge.encode_command({%Paragraph{text: "x", style: :bad}, rect})
    end

    assert_raise ArgumentError, "invalid color value: {:bad}", fn ->
      Bridge.encode_command({%Paragraph{text: "x", style: %Style{fg: {:bad}}}, rect})
    end

    assert_raise ArgumentError, "invalid layout constraint: {:bogus, 1}", fn ->
      Bridge.encode_command({%Table{rows: [["x"]], widths: [{:bogus, 1}]}, rect})
    end
  end

  test "encode_command accepts snapshot tuples for TextInput state (distributed path)" do
    rect = %Rect{x: 0, y: 0, width: 20, height: 1}
    snapshot = {"hello", 3, 0}
    {widget, _} = Bridge.encode_command({%TextInput{state: snapshot}, rect})

    assert widget["type"] == "text_input"
    assert widget["state"] == {"hello", 3, 0}
  end

  test "encode_command accepts snapshot tuples for Textarea state (distributed path)" do
    rect = %Rect{x: 0, y: 0, width: 20, height: 5}
    snapshot = {"line1\nline2", 1, 3}
    {widget, _} = Bridge.encode_command({%Textarea{state: snapshot}, rect})

    assert widget["type"] == "textarea"
    assert widget["state"] == {"line1\nline2", 1, 3}
  end

  test "snapshot tuple roundtrips through Session.draw without error" do
    session = Session.new(20, 5)

    widgets = [
      {%TextInput{state: {"world", 5, 0}}, %Rect{x: 0, y: 0, width: 20, height: 1}},
      {%Textarea{state: {"abc\ndef", 1, 2}}, %Rect{x: 0, y: 1, width: 20, height: 4}}
    ]

    assert :ok = Session.draw(session, widgets)
    assert byte_size(Session.take_output(session)) > 0
    Session.close(session)
  end

  test "encode_command supports min, max, ratio, and fill column widths" do
    {widget, _rect} =
      Bridge.encode_command(
        {%Table{rows: [["x"]], widths: [{:min, 1}, {:max, 2}, {:ratio, 1, 3}, {:fill, 1}]},
         %Rect{x: 0, y: 0, width: 10, height: 2}}
      )

    assert widget["widths"] == [
             %{"type" => "min", "value" => 1},
             %{"type" => "max", "value" => 2},
             %{"type" => "ratio", "num" => 1, "den" => 3},
             %{"type" => "fill", "value" => 1}
           ]
  end

  # Column widths route through the shared Layout.encode_constraint/1, so a
  # {:fill, _} width must draw end-to-end. Regression for the crash where the
  # bridge's own encoder lacked :fill and blew up inside draw/2 (not render).
  test "a Table with {:fill, weight} column widths draws without crashing" do
    terminal = ExRatatui.init_test_terminal(30, 3)
    on_exit(fn -> ExRatatui.Native.restore_terminal(terminal) end)

    table = %Table{
      header: ["name", "value"],
      rows: [["alpha", "1"], ["beta", "2"]],
      widths: [{:length, 8}, {:fill, 1}]
    }

    assert :ok = ExRatatui.draw(terminal, [{table, %Rect{x: 0, y: 0, width: 30, height: 3}}])
    assert ExRatatui.get_buffer_content(terminal) =~ "alpha"
  end
end
